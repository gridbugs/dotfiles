# Maintainer: Jose Riha <jose1711 gmail com>
# Contributor: Patrick Jackson <PatrickSJackson gmail com>
# Contributor: Christoph Vigano <mail@cvigano.de>

pkgname=st
pkgver=0.8.4
pkgrel=2
pkgdesc='A simple virtual terminal emulator for X.'
arch=('i686' 'x86_64' 'armv7h')
license=('MIT')
depends=('libxft' 'libxext')
makedepends=('ncurses')
url="https://st.suckless.org"
source=(https://dl.suckless.org/st/$pkgname-$pkgver.tar.gz
        https://st.suckless.org/patches/bold-is-not-bright/st-bold-is-not-bright-20190127-3be4cf1.diff
        https://st.suckless.org/patches/scrollback/st-scrollback-20201205-4ef0cbd.diff
        https://st.suckless.org/patches/scrollback/st-scrollback-mouse-20191024-a2c479c.diff
        https://st.suckless.org/patches/scrollback/st-scrollback-mouse-altscreen-20200416-5703aa0.diff
        usercflags.diff
        config.h
        )
sha256sums=('d42d3ceceb4d6a65e32e90a5336e3d446db612c3fbd9ebc1780bc6c9a03346a6'
            '329169acac7ceaf901995d6e0897913089b799d8cd150c7f04c902f4a5b8eab2'
            '3b8c7d1815352cbfa2e100f6bb65e4c7d5a338952a6e7513b59a6a6297f32fb4'
            '319458d980195d18fa0f81a6898d58f8d046c5ff982ab872d741f54bb60e267d'
            'cb87eb654985da46ff63663407184402393ad3d3013c8795570552fe56a15b9d'
            'SKIP'
            'SKIP'
            )

prepare() {
  # user is supposed to maintain config.h him/herself
  cp $srcdir/config.h $srcdir/$pkgname-$pkgver/config.h
}

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -p1 -d $srcdir/$pkgname-$pkgver < $srcdir/st-bold-is-not-bright-20190127-3be4cf1.diff
  patch -p1 -d $srcdir/$pkgname-$pkgver < $srcdir/st-scrollback-20201205-4ef0cbd.diff
  patch -p1 -d $srcdir/$pkgname-$pkgver < $srcdir/st-scrollback-mouse-20191024-a2c479c.diff
  patch -p1 -d $srcdir/$pkgname-$pkgver < $srcdir/st-scrollback-mouse-altscreen-20200416-5703aa0.diff
  patch -p1 -d $srcdir/$pkgname-$pkgver < $srcdir/usercflags.diff
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 \
      USERCFLAGS=-DUSERFONT="\"\\\"xos4 Terminus:pixelsize=${FONTSIZE:-16}:antialias=true:autohint=true\\\"\""
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
  # remove to avoid conflict with ncurses
  rm "${pkgdir}/usr/share/terminfo/s/st" "${pkgdir}/usr/share/terminfo/s/st-256color"
}
