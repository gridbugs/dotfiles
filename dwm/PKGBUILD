# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Dag Odenhall <dag.odenhall@gmail.com>
# Contributor: Grigorios Bouzakis <grbzks@gmail.com>

pkgname=dwm
pkgver=6.2
pkgrel=1
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft' 'freetype2' 'st' 'dmenu')
install=dwm.install
source=(https://dl.suckless.org/dwm/dwm-$pkgver.tar.gz
        https://dwm.suckless.org/patches/noborder/dwm-noborder-$pkgver.diff
        https://dwm.suckless.org/patches/pertag/dwm-pertag-$pkgver.diff
        https://dwm.suckless.org/patches/fibonacci/dwm-fibonacci-$pkgver.diff
        dwm.desktop
        bottomstack-custom.diff
        usercflags.diff
        config.h
        )
sha256sums=('97902e2e007aaeaa3c6e3bed1f81785b817b7413947f1db1d3b62b8da4cd110e'
            '9bbf5f963e5a2d23ae4b8731f0c179a8615de5715a2dbf683fbe02115e24efe0'
            '055da0f12dbfde9e50df54e1f2d87966466404a36c056efb94bb21ab03b94b10'
            '2d1176728fc04308ab97cb9f2b628fd00038b2fb4162e84de339164fed9cc48b'
            'bc36426772e1471d6dd8c8aed91f288e16949e3463a9933fee6390ee0ccd3f81'
            'SKIP'
            'SKIP'
            'SKIP'
            )

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  cp "$srcdir/config.h" config.h
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 -d $srcdir/$pkgname-$pkgver < $srcdir/dwm-fibonacci-$pkgver.diff
  patch -p1 -d $srcdir/$pkgname-$pkgver < $srcdir/usercflags.diff
  patch -p1 -d $srcdir/$pkgname-$pkgver < $srcdir/bottomstack-custom.diff
  patch -p1 -d $srcdir/$pkgname-$pkgver < $srcdir/dwm-pertag-$pkgver.diff
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 FREETYPEINC=/usr/include/freetype2 \
      USERCFLAGS=-DUSERFONT="\"\\\"xos4 Terminus:pixelsize=${FONTSIZE:-16}:antialias=true:autohint=true\\\"\""
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -m644 -D README "$pkgdir/usr/share/doc/$pkgname/README"
  install -m644 -D "$srcdir/dwm.desktop" "$pkgdir/usr/share/xsessions/dwm.desktop"
}
